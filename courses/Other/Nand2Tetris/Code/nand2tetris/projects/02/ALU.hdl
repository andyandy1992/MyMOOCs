// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
   // Put you code here:
   //SPECIFY x and y
   // if (zx==1) set x = 0
   Mux16(a=x, b=false, sel=zx, out=newx);
      
   // if (nx==1) set x = ~x
   Not16(in=newx, out=N.newx);
   Mux16(a=newx, b=N.newx, sel=nx, out=Newx);
   
   // if (zy==1) set y = 0
    Mux16(a=y, b=false, sel=zy, out=newy);
	
   // if (ny==1) set y = ~y
   Not16(in=newy, out=N.newy);
   Mux16(a=newy, b=N.newy, sel=ny, out=Newy);
   
   //ADD or AND?
   // if (f==1)  set out = x + y
   // if (f==0)  set out = x & y
   Add16(a=Newx, b=Newy, out=x.P.y);
   And16(a=Newx, b=Newy, out=A.x.y);
   Mux16(a=A.x.y, b=x.P.y, sel=f, out=add.or.and);
   
   //INVERT? 
   // if (no==1) set out = ~out
   Not16(in=add.or.and, out=N.add.or.and);
   Mux16(a=add.or.and, b=N.add.or.and, sel=no, out=out, out[15]=MSB, out[0..7]=out1, out[8..15]=out2);
   
   //ZERO FLAG
   // if (out==0) set zr = 1
   Or8Way(in=out1, out=out.or1);
   Or8Way(in=out2, out=out.or2);
   Or(a=out.or1, b=out.or2, out=out.or);
   Xor(a=out.or, b=true, out=zr);
   
   //NEGATIVE FLAG
   // if (out<0) set ng = 1
	And(a=MSB, b=true, out=ng);
}